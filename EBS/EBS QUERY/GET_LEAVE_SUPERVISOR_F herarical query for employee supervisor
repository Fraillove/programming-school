CREATE OR REPLACE FUNCTION GET_LEAVE_SUPERVISOR_F(p_transaction_id IN VARCHAR2)
  RETURN VARCHAR2 IS

  CUR_PERSON_ID  VARCHAR2(200);
  SUP_PERSON_ID  VARCHAR2(200);
  LEAVE_APPROVAL VARCHAR2(200);
  COUNT_LEVEL    NUMBER NOT NULL DEFAULT 0;

  --// function that will return supervisor id and leave approval

  FUNCTION GET_SUPERVISOR_ID(FIL_PERSOR_ID      IN VARCHAR2,
                             FOL_LEAVE_APPROVAL OUT VARCHAR2) RETURN VARCHAR2 IS
    FL_SUP_ID VARCHAR2(200);

  BEGIN

    SELECT DISTINCT PAAF.SUPERVISOR_ID, nvl(paaf.ASS_ATTRIBUTE16, 'N')
      INTO FL_SUP_ID, FOL_LEAVE_APPROVAL
      FROM per_all_people_f per, PER_ALL_ASSIGNMENTS_F PAAF
     WHERE 1 = 1
       AND PAAF.PERSON_ID = PER.PERSON_ID
       AND SYSDATE BETWEEN per.effective_start_date AND
           per.effective_end_date
       AND SYSDATE BETWEEN pAAF.effective_start_date AND
           pAAF.effective_end_date
       AND PER.PERSON_ID = FIL_PERSOR_ID;
    --// IF SUPERVISOR HAVE LEAVE APPROVAL RIGHTS THEN
    IF FOL_LEAVE_APPROVAL = 'Y' THEN
      RETURN FIL_PERSOR_ID;
      --// IF SUPPEVISOR NOT FOUND THEN RETURN NULL
    ELSIF FL_SUP_ID is NULL THEN
      FOL_LEAVE_APPROVAL := 'F';
      RETURN NULL;
    ELSE
      -- IF SUPERVISOR IS FOUND AND NOT HAVE APPROVAL RIGHTS
      RETURN FL_SUP_ID;
    END IF;

  EXCEPTION
    WHEN OTHERS THEN
      FOL_LEAVE_APPROVAL := 'F';
      RETURN NULL;
  END;

BEGIN

  --// Get person id  of Current Transaction
  begin
    select s.SELECTED_PERSON_ID
      into CUR_PERSON_ID
      from hr_api_transactions s
     where s.TRANSACTION_ID = p_transaction_id;
  end;

  SUP_PERSON_ID := GET_SUPERVISOR_ID(CUR_PERSON_ID, LEAVE_APPROVAL);

  IF LEAVE_APPROVAL = 'N' THEN
    --//FIND SUPERVISOR THAT HAVE LEAVE APPROVAL
    LOOP
      EXIT WHEN LEAVE_APPROVAL IN('Y', 'F');
      SUP_PERSON_ID := GET_SUPERVISOR_ID(SUP_PERSON_ID, LEAVE_APPROVAL);
      COUNT_LEVEL   := COUNT_LEVEL + 1;

      DBMS_OUTPUT.PUT_LINE(SUP_PERSON_ID || ' Level: ' || COUNT_LEVEL ||
                           ' App: ' || LEAVE_APPROVAL);

    END LOOP;
  END IF;
  RETURN SUP_PERSON_ID;

END;
